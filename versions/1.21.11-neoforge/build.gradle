plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.139'
}

version = '1.2.0'
group = 'com.jellycreative.soullink'

base {
    archivesName = 'soullink-1.21.11-neoforge'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

neoForge {
    version = '21.11.25-beta'
    
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', 'soullink'
        }
        
        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', 'soullink'
        }
        
        gameTestServer {
            type = 'gameTestServer'
            systemProperty 'neoforge.enabledGameTestNamespaces', 'soullink'
        }
        
        data {
            data()
            programArguments.addAll '--mod', 'soullink', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
        }
    }
    
    mods {
        soullink {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version: '1.21.11',
            minecraft_version_range: '[1.21.11,1.22)',
            neo_version: '21.11.29-beta',
            neo_version_range: '[21.11,)',
            loader_version_range: '[4,)',
            mod_id: 'soullink',
            mod_name: 'Soul-Link',
            mod_license: 'MIT',
            mod_version: version,
            mod_authors: 'JellyCreative',
            mod_description: 'Links health, hunger, knockback, and inventory between all players.'
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "soullink",
                "Specification-Vendor"    : "JellyCreative",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "JellyCreative",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
